apply plugin: 'download-task'
apply plugin: 'kotlin' // we don't actually need this plugin, but IDEA won't even highlight kotlin syntax properly without it

// relative to 'frontend' dir
def JS_PATH = '../src/main/resources/beholder/backend/web'

def KOTLINC_ZIP_URL = 'https://github.com/JetBrains/kotlin/releases/download/build-0.8.11/kotlin-compiler-0.8.11.zip'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.undercouch:gradle-download-task:1.+'
    }
}

tasks.assemble.dependsOn = [] // essentially turning kotlin plugin off
repositories {
    mavenCentral()
}
dependencies {
    compile 'org.jetbrains.kotlin:kotlin-js-library:0.8.11'
}

tasks.clean.dependsOn 'cleanFrontend'
task cleanFrontent {
    dependsOn 'cleanBundledKotlinJs'
    dependsOn 'cleanCompiledJs'
    doLast {
        delete 'build'
    }
}

task downloadKotlinCompiler {
    outputs.upToDateWhen {file('build/kotlin-compiler.zip').exists()}
    doLast {
        download {
            src  KOTLINC_ZIP_URL
            dest file('build/kotlin-compiler.zip')
        }
    }
}

def libJsFiles = ['kotlin-lib.js', 'kotlin-lib-ecma5.js', 'kotlin-maps.js']
task copyBundledKotlinJs {
    outputs.upToDateWhen { libJsFiles.find {!file(JS_PATH + '/' + it).exists()} == null }
    doLast {
        def jsLib = project.configurations.compile.find { it.name.startsWith("kotlin-js-library-") }
        copy {
            from zipTree(file(jsLib))
            include libJsFiles
            into JS_PATH
        }
    }
}
task cleanBundledKotlinJs {
    doLast {
        libJsFiles.each {delete JS_PATH + '/' + it}
    }
}

task setupKotlinJsCompiler {
    dependsOn 'downloadKotlinCompiler'
    outputs.upToDateWhen {file('build/kotlinc/bin/kotlinc-js').exists()}
    doLast {
        def zipfile = file('build/kotlin-compiler.zip')
        assert zipfile.exists(), "Downloading kotlin compiler failed"

        copy {
            from zipTree(zipfile)
            into 'build'
        }

        assert file('build/kotlinc/bin/kotlinc-js').exists(), 'Downloaded kotlin compiler is corrupt'
    }
}

task compileJs {
    dependsOn 'setupKotlinJsCompiler'
    dependsOn 'copyBundledKotlinJs'
    outputs.upToDateWhen {
        ArrayList<File> files = []
        file('src/main/kotlin').eachFileRecurse {
            if (it.name.endsWith('.kt') || it.isDirectory()) {
                files.add it
            }
        }
        def lastModTime = files.inject(null, {
            time, file ->
                def lastModified = file.lastModified()
                return (time == null || time < lastModified) ? lastModified : time
        })
        return lastModTime != null && lastModTime < file("${JS_PATH}/script.js").lastModified()
    }
    doLast {
        def jsLib   = project.configurations.compile.find { it.name.startsWith("kotlin-js-library-") }
        def kotlinc = file('build/kotlinc/bin/kotlinc-js').canonicalPath
        def out     = file("${JS_PATH}/script.js").canonicalPath
        def source  = file('src/main/kotlin').canonicalPath

        // TODO proper argument escaping if possible
        def command = "${kotlinc} -sourcemap -output ${out} -libraryFiles ${jsLib} -sourceFiles ${source}"

        def proc = command.execute()
        proc.waitFor()
        assert proc.exitValue() == 0, "Command failed: ${command}\nCommand output:\n${proc.text}"
    }
}
task cleanCompiledJs {
    doLast {
        delete "${JS_PATH}/script.js"
        delete "${JS_PATH}/script.js.map"
    }
}
